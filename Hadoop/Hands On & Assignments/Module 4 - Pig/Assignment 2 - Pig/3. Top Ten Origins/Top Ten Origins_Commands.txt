flight_details = load 'flights_details.csv' USING PigStorage(',') AS (SerialNo:int, Year:chararray, Month:chararray, DayofMonth:chararray, DayofWeek:int, DepTime:chararray, CRSDepTime:chararray, ArrTime:chararray, CRSArrTime:chararray, UniqueCarrier:chararray, FlightNum:chararray, TailNum:chararray, ActualElapsedTime:chararray, CRSElapsedTime:chararray, AirTime:chararray, ArrDelay:int, DepDelay:int, Origin:chararray, Destination:chararray, Distance:int, TaxiIn:int, TaxiOut:int, Cancelled:int, CancellationCode:chararray, Diverted:int, CarrierDelay:chararray, WeatherDelay:chararray, NASDelay:chararray, SecurityDelay:chararray, LateAircraftDelay:chararray);

valid_flights = FILTER flight_details BY (Cancelled == 0) AND (DepDelay IS NOT NULL);

grouped_by_origin = GROUP valid_flights BY Origin;

origin_avg_depdelay = FOREACH grouped_by_origin GENERATE group AS Origin, AVG(valid_flights.DepDelay) AS avg_depdelay;

ordered_avg_depdelay = ORDER origin_avg_depdelay BY avg_depdelay DESC;

top_ten_origins = LIMIT ordered_avg_depdelay 10;

STORE top_ten_origins INTO 'top_ten_origins' USING PigStorage(',');

DUMP top_ten_origins;